module picklr.aws.CloudFront

import "../Resource.pkl"

/// A CloudFront Distribution.
class Distribution extends Resource.Resource {
  provider = "aws"

  /// Whether the distribution is enabled to accept end user requests for content.
  enabled: Boolean = true

  /// Valid values: "PriceClass_100", "PriceClass_200", "PriceClass_All".
  priceClass: String = "PriceClass_100"

  /// Default cache behavior.
  defaultCacheBehavior: DefaultCacheBehavior

  /// Ordered cache behaviors (for path-based routing).
  orderedCacheBehaviors: Listing<OrderedCacheBehavior>?

  /// Origins.
  origins: Listing<Origin>

  properties = new Mapping {
    ["enabled"] = enabled
    ["price_class"] = priceClass
    ["default_cache_behavior"] = new Mapping {
      ["target_origin_id"] = defaultCacheBehavior.targetOriginId
      ["viewer_protocol_policy"] = defaultCacheBehavior.viewerProtocolPolicy
      ["allowed_methods"] = defaultCacheBehavior.allowedMethods
    }
    ["ordered_cache_behaviors"] = 
      if (orderedCacheBehaviors != null)
        orderedCacheBehaviors
          .toList()
          .map((cb) -> new Mapping {
            ["path_pattern"] = cb.pathPattern
            ["target_origin_id"] = cb.targetOriginId
            ["viewer_protocol_policy"] = cb.viewerProtocolPolicy
            ["allowed_methods"] = cb.allowedMethods
          })
      else null

    ["origins"] =
      origins
        .toList()
        .map((o) -> new Mapping {
          ["domain_name"] = o.domainName
          ["origin_id"] = o.originId
        })
  }
}

class DefaultCacheBehavior {
  targetOriginId: String
  viewerProtocolPolicy: String = "allow-all"
  allowedMethods: Listing<String> = new Listing { "GET"; "HEAD" }
}

class OrderedCacheBehavior {
  pathPattern: String
  targetOriginId: String
  viewerProtocolPolicy: String = "allow-all"
  allowedMethods: Listing<String> = new Listing { "GET"; "HEAD" }
}

class Origin {
  domainName: String
  originId: String
}
